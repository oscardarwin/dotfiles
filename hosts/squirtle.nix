{ stylix, nixos-hardware, makeNixosModules, importHomeModules, importNixosModules, ... }: makeNixosModules {

  users.oscar = {
    home.stateVersion = "21.11";
    imports = importHomeModules [
      "fonts.nix"
      "firefox.nix"
      "git.nix"
      "sway"
      "nixvim"
      "startup.nix"
      "shell.nix"
      "terminal.nix"
      "screen.nix"
      "qutebrowser"
      "packages.nix"
      "stylix.nix"
      "wofi.nix"
      "social_media.nix"
    ] ++ [
      stylix.homeModules.stylix
    ];
  };

  nixosModules = importNixosModules [
    "bootloader.nix"
    "ssh.nix"
    "audio.nix"
    "networking.nix"
    "locale.nix"
    "screensharing.nix"
    "docker.nix"
    "greetd.nix"
  ] ++ [
    nixos-hardware.nixosModules.microsoft-surface-laptop-amd
  ];

  config = {
    # added so that regreet can read the generated session (which is not generated in home manager)
    programs.sway = {
      enable = true;
      wrapperFeatures.gtk = true;
    };
    programs.dconf.enable = true;

    services = {
      printing.enable = true;
      # Configure keymap in X11 -- can remove??
      xserver.xkb = {
        layout = "us";
        variant = "";
      };
    };

    users.users.oscar = {
      isNormalUser = true;
      description = "oscar";
      extraGroups = [ "networkmanager" "wheel" ];
    };

    security.polkit.enable = true;

    # This value determines the NixOS release from which the default
    # settings for stateful data, like file locations and database versions
    # on your system were taken. It‘s perfectly fine and recommended to leave
    # this value at the release version of the first install of this system.
    # Before changing this value read the documentation for this option
    # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
    system.stateVersion = "24.05"; # Did you read the comment?
  };

  hardware = { pkgs, config, lib, modulesPath, ... }: {
    # Do not modify this attribute set!  It was generated by ‘nixos-generate-config’
    # and may be overwritten by future invocations.  Please make changes
    # to /etc/nixos/configuration.nix instead.
    imports =
      [
        (modulesPath + "/installer/scan/not-detected.nix")
      ];

    boot.initrd.availableKernelModules = [ "nvme" "xhci_pci" "usb_storage" "sd_mod" "amdgpu" ];
    boot.initrd.kernelModules = [ ];
    boot.kernelModules = [ ];
    boot.extraModulePackages = [ ];
    boot.kernelParams = [ "amd_iommu=off" ];
    boot.kernelPackages =
      let
        kernelPkgSet = pkgs.linuxPackages_latest;
      in
      lib.mkForce (kernelPkgSet // {
        kernel = kernelPkgSet.kernel.overrideAttrs (old: {
          rustKernel = false; # or pin rust = pkgs.rust-bin.stable_1_87
        });
      });

    # optimise battery life
    services.tlp = {
      enable = true;
    };

    services.xserver.videoDrivers = [ "modesetting" ];
    hardware.graphics.enable = true;
    hardware.graphics.enable32Bit = true;

    hardware.bluetooth.enable = true;
    hardware.bluetooth.powerOnBoot = true;

    fileSystems."/" =
      {
        device = "/dev/disk/by-uuid/f9784ddc-018e-4e7c-9778-d782417cbf09";
        fsType = "ext4";
      };

    fileSystems."/boot" =
      {
        device = "/dev/disk/by-uuid/72D8-0FBD";
        fsType = "vfat";
        options = [ "fmask=0077" "dmask=0077" ];
      };

    swapDevices = [ ];

    # Enables DHCP on each ethernet and wireless interface. In case of scripted networking
    # (the default) this is the recommended approach. When using systemd-networkd it's
    # still possible to use this option, but it's recommended to use it in conjunction
    # with explicit per-interface declarations with `networking.interfaces.<interface>.useDHCP`.
    networking.useDHCP = lib.mkDefault true;
    # networking.interfaces.wlp2s0.useDHCP = lib.mkDefault true;

    nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
    hardware.cpu.amd.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
  };
}
